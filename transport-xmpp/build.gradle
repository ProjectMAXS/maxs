buildscript {
	repositories {
		mavenCentral()
		google()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:3.1.4'
	}
}

def keystoreProperties = new Properties()

def userKeystorePropertiesFile = new File(System.getProperty("user.home"), ".config/maxs/keystore.properties")
if (userKeystorePropertiesFile.exists()) {
	keystoreProperties.load(new FileInputStream(userKeystorePropertiesFile))
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
	keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def keystorePropertiesFileString = System.getProperty("keystorePropertiesFile")
if (keystorePropertiesFileString != null) {
	keystoreProperties.load(new FileInputStream(file(keystorePropertiesFileString)))
}

apply plugin: 'com.android.application'

repositories {
  mavenCentral()
  google()
  jcenter()
}

android {
	compileSdkVersion 24
	buildToolsVersion '28.0.1'

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			aidl.srcDirs = ['src']
			res.srcDirs = ['res']
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	signingConfigs {
		if (keystoreProperties.getProperty('keyPassword') != null) {
			release {
				keyAlias keystoreProperties['keyAlias']
				keyPassword keystoreProperties['keyPassword']
				storeFile file(keystoreProperties['storeFile'])
				storePassword keystoreProperties['storePassword']
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'),
				'proguard-project.txt'
			signingConfig signingConfigs.release
		}
		debug {
			minifyEnabled false
		}
	}

}

ext {
	smackVersion = '4.3.0'
	minidnsVersion = '0.3.2'
	iespVersion = '0.0.1'
}

dependencies {
	implementation "eu.geekplace.iesp:iesp-aar:$iespVersion"
	implementation "org.igniterealtime.smack:smack-android-extensions:$smackVersion"
	implementation "org.igniterealtime.smack:smack-experimental:$smackVersion"
	implementation "org.igniterealtime.smack:smack-tcp:$smackVersion"
	implementation "org.minidns:minidns-dnssec:$minidnsVersion"
	implementation project(':memorizingTrustManager')
}

configurations {
	all*.exclude group: 'xpp3', module: 'xpp3'
}

task makePrebuild(type:Exec) {
	commandLine 'make', 'prebuild', 'MAXS_BUILD_SYSTEM=gradle'
}

preBuild.dependsOn makePrebuild
