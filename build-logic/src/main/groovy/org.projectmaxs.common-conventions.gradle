plugins {
	id 'com.android.application'
}

def keystoreProperties = new Properties()

def userKeystorePropertiesFile = new File(System.getProperty("user.home"), ".config/maxs/keystore.properties")
if (userKeystorePropertiesFile.exists()) {
	keystoreProperties.load(new FileInputStream(userKeystorePropertiesFile))
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
	keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def keystorePropertiesFileString = System.getProperty("keystorePropertiesFile")
if (keystorePropertiesFileString != null) {
	keystoreProperties.load(new FileInputStream(file(keystorePropertiesFileString)))
}

// Which may be overriden by a module individial module-settings.gradle file.
def moduleSettingsFile = rootProject.file("module-settings.gradle")
if (moduleSettingsFile.exists()) {
	apply from: moduleSettingsFile
}

repositories {
  mavenCentral()
  google()
}

ext {
	signingKeystoreAvailable = keystoreProperties.getProperty('keyPassword') != null
	javaVersion = JavaVersion.VERSION_11
}

android {
	compileSdkVersion 35

	defaultConfig {
		minSdkVersion 19
		// MAXS' modular architecture consisting of communicating
		// background services makes it hard to adopt to newer API
		// levels which impose more and more restrictions on
		// background processing and inter-app communication.
		targetSdkVersion 24
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			aidl.srcDirs = ['src']
			res.srcDirs = ['res']
		}
	}

	compileOptions {
		sourceCompatibility javaVersion
		targetCompatibility javaVersion
	}

	buildFeatures {
		aidl = true
	}

	signingConfigs {
		if (signingKeystoreAvailable) {
			release {
				keyAlias keystoreProperties['keyAlias']
				keyPassword keystoreProperties['keyPassword']
				storeFile file(keystoreProperties['storeFile'])
				storePassword keystoreProperties['storePassword']
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			if (signingKeystoreAvailable) {
				signingConfig signingConfigs.release
			}
		}
		debug {
			minifyEnabled false
		}
	}

}

dependencies {
	implementation 'eu.geekplace.iesp:iesp-aar:0.0.2'
}

task makePrebuild(type:Exec) {
	commandLine 'make', 'prebuild'
}

preBuild.dependsOn makePrebuild
